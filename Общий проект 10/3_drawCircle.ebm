macro_definition_begin
    "id": "3"
    "name": "drawCircle"
    "startup": "false"
    "periodic": [ "true", "20" ]
    "interlock": [ "false" ]
macro_definition_end
//Отрисовка кружочка
macro_command main()

	short network_error=0,position_error=0,nullObject=0
	char clear_bit=0
	unsigned short paint_data[10]={0},dev_model=0
	unsigned short Centr_X_g=0,Centr_Y_g=0, radiusx=0, radiusy=0

	float Centr_X_f=0, Centr_Y_f=0,c_x=0,c_y=0, dx=0, dy=0
	float f_cos=0, f_sin=0

	int currentWindow = 0
	int temp=0
	float zonePxSize = 230
	float mmToPx1=0,mmToPx2=0
	//Понимаем на каком мы окне
	GetData(currentWindow, "Local HMI", LW, 9050, 1)
	currentWindow=currentWindow-29
	if currentWindow <> 1 and currentWindow <> 2 and currentWindow <> 3 then
		return
	end if
		
	temp=currentWindow*10
	GetData(network_error, "Local HMI", LB, temp, 1)
	temp=temp+1
	GetData(position_error, "Local HMI", LB, temp, 1)
	temp=temp+1
	GetData(nullObject, "Local HMI", LB, temp, 1)
	temp=currentWindow*100+61
	GetData(dev_model, "Local HMI", LW, temp, 1)

	COS(45,f_cos)
	SIN(45,f_sin)

	if network_error == 0 and position_error == 0 and nullObject == 0  then												//Рисуем новый кружок
		temp=currentWindow*100 + 32
		GetData(dx, "Local HMI", LW, temp, 1)
		temp=temp+2
		GetData(dy, "Local HMI", LW, temp, 1)
		temp=temp+2
		GetData(c_x, "Local HMI", LW, temp, 1)
		temp=temp+2
		GetData(c_y, "Local HMI", LW, temp, 1)
		
		mmToPx1=zonePxSize/(2*dev_model)
		radiusx = mmToPx1*dx
		radiusy = mmToPx1*dy
		
		mmToPx2=(zonePxSize)/dev_model
		
		Centr_X_g = (f_cos*c_x- f_sin*c_y)*mmToPx2 + zonePxSize/2
		Centr_Y_g = (f_cos*c_y+f_sin*c_x)*mmToPx2 + zonePxSize/2	
		
		
			
		clear_bit=1
		temp=currentWindow*10+3
		SetData(clear_bit, "Local HMI", LB, temp, 1)
		
		DELAY(50)

		paint_data[0]=5 		//круг
		paint_data[1]=1  		//закрашенный
		paint_data[2]=0			//Сплошной
		paint_data[3]=0			//цвет
		paint_data[4]=5			//образе внутреннего цвета
		paint_data[5]=Centr_X_g	//x1
		paint_data[6]=Centr_Y_g	//y1
		paint_data[7]=radiusx		//Радиус
		paint_data[8]=radiusy		//Радиус
		
		temp=currentWindow*100+50
		SetData(paint_data[0], "Local HMI", LW, temp, 10)
	
	else																		//Рисуем старый кружок
		clear_bit=1
		temp=currentWindow*10+3
		SetData(clear_bit, "Local HMI", LB, temp, 1)

		DELAY(50)
				
		paint_data[0]=5 //3
		paint_data[1]=1  //1
		paint_data[2]=0
		paint_data[3]=0
		paint_data[4]=5
		paint_data[5]=0
		paint_data[6]=0
		paint_data[7]=0
		
		temp=currentWindow*100+50
		SetData(paint_data[0], "Local HMI", LW, temp, 10)
		
	end if
	

end macro_command